"""
   Copyright 2014 The Trustees of Princeton University

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
"""

# build libsyndicate drivers
import os

# driver files 
driver_dirs = Glob("*")

# source search paths
CPPPATH = """
   #build/out/lib/libsyndicate
   #
   #build/out/protobufs/
"""

# libraries 
LIBS = """
   syndicate
"""

# library search paths
LIBPATH = """
   #build/out/lib/libsyndicate/
"""

# add additional CPPFLAGS here
CPPFLAGS = """
   -rdynamic
"""

# add additional CPPDEFINES here
CPPDEFINES = [
   ("_FILE_OFFSET_BITS", 64),
   "_REENTRANT",
   "_THREAD_SAFE"
]

# get our environment
Import('env')
Import("extra_args")

env = env.Clone()
env.Append(LIBPATH = Split(LIBPATH))
env.Append(CPPPATH = Split(CPPPATH))
env.Append(CPPFLAGS = Split(CPPFLAGS))
env.Append(CPPDEFINES = CPPDEFINES)

def create_sobjs(SRCS, path="", this_env=None):
   if this_env is None:
      this_env=env
   
   return [this_env.SharedObject(os.path.join(path, os.path.dirname(src), "driver.so"), path+src) for src in SRCS]

def driver_relpath( path ):
   driver_so_cpp = os.path.basename( path )
   driver_so_name = os.path.basename( os.path.dirname(path).strip("/") )
   return os.path.join( driver_so_name, driver_so_cpp )

driver_libs = []

for driver_dir in driver_dirs:
   if driver_dir.path.endswith("SConscript"):
      continue

   driver_files = Glob( "%s/*.cpp" % os.path.basename(driver_dir.path) )
   driver_sos = create_sobjs( [driver_relpath(x.path) for x in filter( lambda y: y.path.endswith(".cpp"), Split(driver_files) )] )
   
   driver_lib = env.SharedLibrary( os.path.join(os.path.basename(driver_dir.path), "driver.so"), driver_sos, LIBS=Split(LIBS) )

   driver_targets = [driver_lib]
   driver_targets.append( Glob("%s/*.py" % os.path.basename(driver_dir.path)) )

   driver_libs.append( driver_targets )
   
Return( 'driver_libs' )

